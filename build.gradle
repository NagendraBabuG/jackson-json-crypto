import com.github.spotbugs.SpotBugsTask

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0"
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: "com.github.spotbugs"

sourceCompatibility = javaVersion
targetCompatibility = javaVersion


task setVersion {
    if (System.env.BUILD_NUMBER) {
        version = projectVersionMajor + '.' + projectVersionMinor + '.' + System.env.BUILD_NUMBER
    } else {
        version = projectVersionMajor + '.' + projectVersionMinor + '.' + projectVersionBuild
    }
}

jar {
    baseName = projectName
    manifest
            {
                attributes 'Implementation-Title': projectName,
                        'Implementation-Version': version
            }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task createPom {
    doLast {
        pom
        {
            project {
                artifactId 'jackson-json-crypto'
                groupId 'com.codingrodent'
                name 'com.codingrodent.jackson.crypto'
                description 'A Crypto Module for Jackson'
                url 'https://github.com/codesqueak/jackson-json-crypto'
                scm {
                    url 'https://github.com/codesqueak/jackson-json-crypto'
                    connection 'scm:git:git://github.com/codesqueak/jackson-json-crypto.git'
                    developerConnection 'scm:git:ssh://github.com:codesqueak/jackson-json-crypto.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'codesqueak'
                        name 'codesqueak'
                        email 'codesqueak@gmail.com'
                        organizationUrl 'http://www.codesqueak.com'
                        //  organization 'codesqueak'
                    }
                }
            }
        }.writeTo("$buildDir/libs/" + projectName + "-" + version + ".pom")
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.7.9"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

test
        {
            maxParallelForks = 1
            filter
                    {
                        includeTestsMatching "com.codingrodent.jackson.*"
                    }
            jacoco {
                append = false
                destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
                classDumpDir = file("$buildDir/jacoco/classpathdumps")
            }
        }

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination = file("${buildDir}/jacocoHtml")
    }
}

spotbugs {
    sourceSets = [sourceSets.main]
    effort = "max"
    reportLevel = "high"
    ignoreFailures = true
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

check.dependsOn jacocoTestReport
test.dependsOn javadoc
jar.dependsOn createPom
jar.dependsOn setVersion



dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.3'
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '5.3.0.Final'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'javax.el', name: 'javax.el-api', version: '2.2.5'
    //
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.13.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}
